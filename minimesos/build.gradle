apply plugin: "groovy"

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
  }
}
apply plugin: "com.github.johnrengelman.shadow"

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    compile 'com.github.docker-java:docker-java:3.0.0'
    compile 'junit:junit:4.11'
    compile 'com.jayway.awaitility:awaitility:1.6.3'
    compile 'com.mashape.unirest:unirest-java:1.4.8'
    compile 'org.slf4j:slf4j-api:1.7.12'

    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'

    compile 'com.beust:jcommander:1.48'

    compile 'org.glassfish.jersey.core:jersey-client:2.11'

    testCompile "org.mockito:mockito-core:1.+"
    // using guru.nidi as maintenanance of the original project is dropped https://github.com/clarkware/jdepend/pull/9
    testCompile "guru.nidi:jdepend:2.9.5"
}

sourceSets {
    main {
        groovy {
            // this makes the groovy-compiler compile groovy- as well as java-files.
            // Needed, because java is normally compiled before groovy.
            // Since we are using groovy objects from java, we need it the other way round.
            srcDirs = ['src/main/groovy', 'src/main/java']
        }
        java {
            srcDirs = [] // don't compile Java code twice
        }
    }
}

task installMinimesosScript(type: Copy) {
    from "$rootDir/bin/minimesos"
    into "/usr/local/bin"
}

shadowJar {
  relocate 'com.github.dockerjava', 'com.containersol.minimesos.dockerjava'
  relocate 'org.glassfish', 'com.containersol.minimesos.glassfish'
  relocate 'javax.ws', 'com.containersol.minimesos.ws'
  relocate 'com.fasterxml', 'com.containersol.minimesos.fasterxml'
  mergeServiceFiles()
}
